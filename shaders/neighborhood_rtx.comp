#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_ray_query : require
#include "cpu_gpu_shared_config.h"

struct neighbors
{
	uint mNeighbor[NEIGHBOR_LIST_MAX_LENGTH];
};

// ---------------------- lists -----------------------
layout(set =  0, binding = 0) buffer InIndexList  { uint       inIndexList[]; };
layout(set =  1, binding = 0) buffer InPosition   { ivec4       inPosition[]; };
layout(set =  2, binding = 0) buffer InRange      { float          inRange[]; };
layout(set =  3, binding = 0) buffer OutNeighbors { neighbors outNeighbors[]; };
// ----------------------------------------------------

// ------------------- list lengths -------------------
layout(set =  4, binding = 0) buffer InIndexListLength { uint inIndexListLength; };
// ----------------------------------------------------

// -------------- acceleration structure --------------
layout(set =  5, binding = 0) uniform accelerationStructureEXT topLevelAS;
// ----------------------------------------------------

// ------------------ push constants ------------------
layout(push_constant) uniform PushConstants {
	float mRangeScale;
};
// ----------------------------------------------------

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;
void main()
{
	uint id = gl_GlobalInvocationID.x;
	if (id >= inIndexListLength) return;

	uint idx = inIndexList[id];
	vec3 pos = vec3(inPosition[idx].xyz) / POS_RESOLUTION;
	
	// Create a Ray Query and search for all the neighbors
	rayQueryEXT rayQuery;
	vec3 rayOrigin    = pos;
	vec3 rayDirection = vec3(0, 0, 1);
	float tMin        = 0.0;
	float tMax        = 0.01;
	rayQueryInitializeEXT(rayQuery, topLevelAS, gl_RayFlagsNoneEXT, 0xFF, rayOrigin, tMin, rayDirection, tMax);
	while(rayQueryProceedEXT(rayQuery)) {
		uint idN     = rayQueryGetIntersectionInstanceCustomIndexEXT(rayQuery, false);
		uint idxN    = inIndexList[idN];
		vec3 posN    = vec3(inPosition[idxN].xyz) / POS_RESOLUTION;
		float rangeN = inRange[idN] * mRangeScale;
		if (distance(pos, posN) > rangeN) continue;

		uint i = 1u + atomicAdd(outNeighbors[idN].mNeighbor[0], 1u);
		if (i < NEIGHBOR_LIST_MAX_LENGTH) {
			outNeighbors[idN].mNeighbor[i] = id;
		} else {
			outNeighbors[idN].mNeighbor[0] = NEIGHBOR_LIST_MAX_LENGTH - 1u;
		}
	}
}
